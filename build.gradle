plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // Apply javafxplugin for JavaFX support.
    id 'org.openjfx.javafxplugin' version '0.0.11'
    // Apply jlink for building the app.
    id 'org.beryx.jlink' version '2.25.0'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
}

ext.os = org.gradle.internal.os.OperatingSystem.current()
appVersion = project.appVersion
javafxVersion = project.javafxVersion

application {
    // Define the main class for the application.
    mainClass = 'com.megabyte6.sierpinski.App'
    mainModule = "sierpinski"
}

javafx {
    version = "${javafxVersion}"
    modules = [ 'javafx.controls' ]
}

jlink {
    options = [
        '--strip-debug',
        '--compress', '2',
        '--no-header-files',
        '--no-man-pages'
    ]
    launcher {
        noConsole = true
    }
    jpackage {
        appVersion = appVersion
        imageName = "Sierpinski"
        installerName = "Sierpinski"
        icon = "src/main/resources/icon.ico"
        vendor = "Brayden Chan"

        if (os.windows) {
            installerOptions = [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'Sierpinski',
                '--win-shortcut',
                '--win-shortcut-prompt'
            ]
        }
    }
}
